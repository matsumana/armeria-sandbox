buildscript {
    ext.v = { project.property("${it}.version") }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${v('spring-boot')}"
        classpath "com.google.gradle:osdetector-gradle-plugin:${v('gradle-plugin-osdetector')}"
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:${v('gradle-plugin-thrift')}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${v('gradle-plugin-protobuf')}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "info.matsumana.armeria"
    version = "0.8.1-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "com.linecorp.armeria:armeria-bom:${v('armeria')}"
            mavenBom "io.zipkin.brave:brave-bom:${v('zipkin-brave')}"

            // Override version Spring Boot use
            mavenBom "io.netty:netty-bom:${v('netty')}"
            mavenBom "io.micrometer:micrometer-bom:${v('micrometer')}"
        }
        dependencies {
            // Override version Armeria use
            dependency "org.springframework.boot:spring-boot-starter:${v('spring-boot')}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${v('spring-boot')}"
            dependency "org.springframework.boot:spring-boot-starter-test:${v('spring-boot')}"

            // armeria-grpc depends on io.grpc:grpc-protobuf and it depends on x.y-android not x.y-jre
            // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf/1.20.0
            dependency "com.google.guava:guava:${v('guava')}"

            dependency "org.apache.thrift:libthrift:${v('libthrift')}"
            dependency "com.squareup.retrofit2:converter-jackson:${v('retrofit')}"
            dependency "com.github.akarnokd:rxjava2-jdk8-interop:${v('rxjava2-jdk8-interop')}"
            dependency "com.linecorp.centraldogma:centraldogma-client-armeria:${v('central-dogma')}"
        }
    }

    dependencies {
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }
        testImplementation "org.assertj:assertj-core"
    }

    test {
        useJUnitPlatform()
    }
}
