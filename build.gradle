buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.property('spring-boot.version')}"
        classpath "com.google.gradle:osdetector-gradle-plugin:${project.property('gradle-plugin-osdetector.version')}"
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:${project.property('gradle-plugin-thrift.version')}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${project.property('gradle-plugin-protobuf.version')}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "info.matsumana.armeria"
    version = "0.6.1-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "com.linecorp.armeria:armeria-bom:${project.property('armeria.version')}"
            mavenBom "io.zipkin.brave:brave-bom:${project.property('zipkin-brave.version')}"
            mavenBom "io.micrometer:micrometer-bom:${project.property('micrometer.version')}"
        }
        dependencies {
            // To override version which Armeria use
            dependency "org.springframework.boot:spring-boot-starter:${project.property('spring-boot.version')}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${project.property('spring-boot.version')}"
            dependency "org.springframework.boot:spring-boot-starter-test:${project.property('spring-boot.version')}"

            // armeria-grpc depends on io.grpc:grpc-protobuf and it depends on x.y-android not x.y-jre
            // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf/1.20.0
            dependency "com.google.guava:guava:${project.property('guava.version')}"

            dependency "org.apache.thrift:libthrift:${project.property('libthrift.version')}"
            dependency "com.squareup.retrofit2:converter-jackson:${project.property('retrofit.version')}"
            dependency "com.github.akarnokd:rxjava2-jdk8-interop:${project.property('rxjava2-jdk8-interop.version')}"
            dependency "com.linecorp.centraldogma:centraldogma-client-armeria:${project.property('central-dogma.version')}"
        }
    }

    dependencies {
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }
        testImplementation "org.assertj:assertj-core"
    }

    compileJava {
        options.compilerArgs += "-parameters"
    }

    test {
        useJUnitPlatform()
    }
}
