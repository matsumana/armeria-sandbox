buildscript {
    ext {
        thriftGradlePluginVersion = "0.4.1"
        protobufGradlePluginVersion = "0.8.9"
        osdetectorGradlePluginVersion = "1.6.2"

        springBootVersion = "2.1.6.RELEASE"
        armeriaVersion = "0.89.0"
        thriftVersion = "0.12"
        libthriftVersion = thriftVersion + ".0"
        guavaVersion = "28.0-jre"
        retrofitVersion = "2.6.0"
        rxJava2Jdk8InteropVersion = "0.3.7"
        braveVersion = "5.6.6"
        protocVersion = "3.8.0"
        protocGenGrpcJavaVersion = "1.22.0"
        centraldogmaVersion = "0.41.0"
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:${thriftGradlePluginVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
        classpath "com.google.gradle:osdetector-gradle-plugin:${osdetectorGradlePluginVersion}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "info.matsumana.armeria"
    version = "0.5.1-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    // TODO will delete later. Armeria 0.88.0 depends on 4.1.37 but Spring Boot provides 4.1.36
    ext["netty.version"] = "4.1.37.Final"

    dependencyManagement {
        imports {
            mavenBom "com.linecorp.armeria:armeria-bom:${armeriaVersion}"
            mavenBom "io.zipkin.brave:brave-bom:${braveVersion}"
        }
        dependencies {
            // To override version which Armeria use
            dependency "org.springframework.boot:spring-boot-starter:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

            // armeria-grpc depends on io.grpc:grpc-protobuf and it depends on x.y-android not x.y-jre
            // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf/1.20.0
            dependency "com.google.guava:guava:${guavaVersion}"

            dependency "org.apache.thrift:libthrift:${libthriftVersion}"
            dependency "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
            dependency "com.github.akarnokd:rxjava2-jdk8-interop:${rxJava2Jdk8InteropVersion}"
            dependency "com.linecorp.centraldogma:centraldogma-client-armeria:${centraldogmaVersion}"
        }
    }

    dependencies {
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "junit", module: "junit"
        }
        testImplementation "org.assertj:assertj-core"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    compileJava {
        options.compilerArgs += "-parameters"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}
