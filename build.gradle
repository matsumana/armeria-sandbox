buildscript {
    ext {
        thriftGradlePluginVersion = "0.4.1"
        osdetectorGradlePluginVersion = "1.6.2"

        springBootVersion = "2.1.4.RELEASE"
        armeriaVersion = "0.84.0"
        thriftVersion = "0.12"
        thriftFullVersion = thriftVersion + ".0"
        retrofitVersion = "2.5.0"
        rxJava2Jdk8InteropVersion = "0.3.5"
        braveVersion = "5.6.3"
        centraldogmaVersion = "0.39.0"
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:${thriftGradlePluginVersion}"
        classpath "com.google.gradle:osdetector-gradle-plugin:${osdetectorGradlePluginVersion}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "info.matsumana.armeria"
    version = "0.1.1-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "com.linecorp.armeria:armeria-bom:${armeriaVersion}"
            mavenBom "io.zipkin.brave:brave-bom:${braveVersion}"
        }
        dependencies {
            // To override version which Armeria use
            dependency "org.springframework.boot:spring-boot-starter:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

            dependency "org.apache.thrift:libthrift:${thriftFullVersion}"
            dependency "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
            dependency "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
            dependency "com.github.akarnokd:rxjava2-jdk8-interop:${rxJava2Jdk8InteropVersion}"
            dependency "com.linecorp.centraldogma:centraldogma-client-armeria:${centraldogmaVersion}"
        }
    }

    dependencies {
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "junit", module: "junit"
        }
        testImplementation "org.assertj:assertj-core"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    compileJava {
        options.compilerArgs += "-parameters"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}
